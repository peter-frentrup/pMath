Begin("FE`")

FE`InsertSpecial

Begin("FE`InsertSpecial`Private`")

$ThePopup:= $Failed
$AllowEmpty:= False

FE`InsertSpecial()::= FE`InsertSpecial(EvaluationDocument())
Function FE`InsertSpecial(~doc) {
    With(selboxes:=DocumentRead(doc))
    insertSpecialAt(doc, selboxes, Developer`GetDebugMetadata(selboxes))
}

getInputAliasesFor(~, ~)::= CurrentValue(InputAliases)

Function getInputAliasesFor(~selboxes, loc: Language`SourceLocation(~feo:FrontEndObject, ~range)) {
    With(allMacros:= CurrentValue(feo, InputAliases) |> Cases(Except(~ -> ("\[Placeholder]" | "\[SelectionPlaceholder]"))))
    
    If(Match(range, ~x .. ~x)) {
        If(DocumentGet(loc) |> Head === SectionGroup) {
            With(styleTemplates:= FE`CallFrontEnd(FrontEnd`GetStylesList(feo)))
            Return(Join(styleTemplates, allMacros))
        } Else {
            Return(allMacros)
        }
    } Else If(selboxes === "\[Placeholder]" || selboxes === "\[SelectionPlaceholder]") {
        allMacros
    } Else {
        allMacros |>
            Select(Function( #[2] |> 
                Position(~s:String /? StringPosition(s, "\[Placeholder]" | "\[SelectionPlaceholder]") =!= {}) =!= {}))
    }
}

Function insertSpecialAt(~doc, ~selboxes, ~loc) {
    With(allMacros:= getInputAliasesFor(selboxes, loc))

    If($ThePopup =!= $Failed && DocumentGet($ThePopup, 0) =!= $Failed) {
        $AllowEmpty:= True
        CurrentValue($ThePopup, Visible):= True
        SetSelectedDocument($ThePopup)
        Return($ThePopup)
    }

    Local(macroName:= "", matches:= {}, selIndex:= 0)
    $AllowEmpty:= False
    $ThePopup:= FE`AttachBoxes(loc, Bottom, {
        Section(
            BoxData({"\"Insert: \"",FillBox(InputFieldBox(Dynamic(macroName),String,ContinuousAction->True))}),
            "PopupWindowSection",
            SectionMargins -> {{4,4}, {4,2}},
            LineBreakWithin -> False),
        % Section(BoxData(DynamicBox(ToBoxes(selIndex))), 
        %     "PopupWindowSection",
        %     SectionMargins -> {{4,4}, {4,2}},
        %     LineBreakWithin -> False),
        Section(BoxData(DynamicBox(
            If(macroName === "" && !$AllowEmpty) {
                PanelBox(StringForm("Type an `1` name", Button("input alias", $AllowEmpty:=True, BaseStyle -> "Hyperlink")) |> ToBoxes,
                    Appearance -> None,
                    FrameMargins -> {{4,4}, {2,4}})
            } Else {
                With(m:= allMacros |> Select(Function(StringMatch(#[1], macroName ++ ~~~, IgnoreCase->True)))) {
                    If(matches =!= m) {
                        If(1 <= selIndex <= Length(matches)) {
                            selIndex:= m |> Position(matches[selIndex], 1) |> Append({0}) |> Part(1, 1)
                        }
                        matches:= m
                    }
                }
                If(matches === {}) {
                    ToBoxes(StringForm("\[HighSixDoubleQuote]`1`\[HighNineDoubleQuote] not found", macroName))
                } Else {
                    Array(Length(matches)) |> 
                        Map(Function({
                            SetterBox(
                                Dynamic(selIndex, 
                                    {Automatic, 
                                    Function {
                                        DocumentClose($ThePopup)
                                        DocumentApply(doc, matches[#, 2])
                                    }
                                    }),
                                #,
                                {PaneBox(makeAliasBoxes(matches[#, 1]),
                                        ImageSize -> {75, Automatic}, LineBreakWithin -> False), 
                                    PaneBox({makeBoxesVisible(matches[#, 2]), " "},
                                        ImageSize -> {150, Automatic}, 
                                        ImageSizeAction -> "Clip",
                                        LineBreakWithin -> False)},
                                ButtonFrame -> "ListViewItem",
                                FrameMargins -> {4, 2},
                                BoxID -> {"ResultButton", #})
                            })) |>
                        GridBox(GridBoxRowSpacing -> 0)
                }
            }, TrackedSymbols :> {macroName, $AllowEmpty})),
            "PopupWindowSection",
            ButtonBoxOptions -> { ButtonFrame -> "ListViewItem" },
            SectionMargins -> 0,
            LineBreakWithin -> False)},
        WindowFrame -> "ThinFrameCallout",
        DocumentEventActions -> {
            "EscapeKeyDown" -> Function(DocumentClose($ThePopup);Break()),
            "UpArrowKeyDown" -> Function {
                If(selIndex > 1) {
                    selIndex-= 1
                    With(id := {"ResultButton", selIndex})
                    With(popup:= $ThePopup)
                    FE`CallFrontEnd(Block {
                        With(newbox:= FrontEnd`BoxReferenceBoxObject(FE`BoxReference(popup, {id})))
                        FrontEnd`ScrollIntoView(newbox)
                    })
                }
                Break()
            },
            "DownArrowKeyDown" -> Function {
                If(selIndex < Length(matches)) {
                    selIndex+= 1
                    With(id := {"ResultButton", selIndex})
                    With(popup:= $ThePopup)
                    FE`CallFrontEnd(Block {
                        With(newbox:= FrontEnd`BoxReferenceBoxObject(FE`BoxReference(popup, {id})))
                        FrontEnd`ScrollIntoView(newbox)
                    })
                }
                Break()
            },
            "ReturnKeyDown" -> Function {
                If(1 <= selIndex <= Length(matches)) {
                    DocumentClose($ThePopup)
                    DocumentApply(doc, matches[selIndex, 2])
                    Break()
                } Else If(macroName === "") {
                    $AllowEmpty:= True
                } Else {
                    With(apply := Lookup(matches, macroName, $Failed))
                    If(apply =!= $Failed) {
                        DocumentClose($ThePopup)
                        DocumentApply(doc, apply)
                        Break()
                    }
                }
            }
        } ,
        RemovalConditions -> {"MouseClickOutsidePopup", "SelectionExit"})
    SetSelectedDocument($ThePopup)
}

makeAliasBoxes(~s)::= s |> StringReplace(" " -> "\(StyleBox(\"\[U+2423]\", FontColor -> GrayLevel(0.5))\)") |> ToBoxes

makeBoxesVisible(~box)::= box
makeBoxesVisible(ButtonBox(~~~b))::= ButtonBox(b, ButtonFrame -> Automatic)
makeBoxesVisible(~s:Section)::= makeSectionVisible(s)
Function makeBoxesVisible(~s:String) {
    With(chars:= ToCharacterCode(s))
    If(Length(chars) === 1 || (Length(chars) === 2 && 16^^D800 <= chars[1] <= 16^^DBFF)) {
        Return({
            PaneBox(s, ImageSize -> {25, Automatic}, LineBreakWithin -> False), 
            StyleBox(InputForm(s) |> ToString, FontColor -> Gray)})
    } Else {
        s
    }
}

makeSectionVisible(~s)::= s
makeSectionVisible(Section("", ~stylename, ~~~opts))::= StyleBox(TextData(stylename), stylename, opts)
makeSectionVisible(Section({}|BoxData({}|""), ~stylename, ~~~opts))::= StyleBox(stylename, stylename, opts)
makeSectionVisible(Section(~content, ~~styles))::= StyleBox(TextData(content), styles)
makeSectionVisible(Section(TextData(~~~content), ~~styles))::= StyleBox(TextData(content), styles)
makeSectionVisible(Section(BoxData(~~~content), ~~styles))::= StyleBox(BoxData(content), styles)

End()

End()
