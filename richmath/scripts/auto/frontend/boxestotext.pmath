
Begin("FE`Private`")

FE`BoxesToText(~s:String)::= s

FE`BoxesToText(~boxes)::= innerBoxesToText(boxes)

parenthesize(~x:String)::= x
parenthesize({~x})::= parenthesize(x)
parenthesize(~x)::= "(" ++ innerBoxesToText(x) ++ ")"

innerBoxesToText("\u2192"):= "->";
innerBoxesToText("\u2254"):= ":=";
innerBoxesToText("\u29F4"):= ":>";
innerBoxesToText("\u2A74"):= "::=";
innerBoxesToText("\u2260"):= "!=";
innerBoxesToText("\u2264"):= "<=";
innerBoxesToText("\u2265"):= ">=";
innerBoxesToText("\u27E6"):= "[[";
innerBoxesToText("\u27E7"):= "]]";

/*  TODO: {"a1", "b"}  becomes  "a1b"  but should be "a1 b" */
innerBoxesToText(~b:List)::= StringExpression @@ b.Map(innerBoxesToText)
innerBoxesToText(~b:/\/)::= StringExpression @@ b.Map(innerBoxesToText)
innerBoxesToText({"\[Piecewise]", ~~b})::= "Piecewise(" ++ innerBoxesToText({b}) ++ ")"

innerBoxesToText(SqrtBox(~x))::= "Sqrt(" ++ innerBoxesToText(x) ++ ")"
innerBoxesToText(RadicalBox(~x,~y))::= innerBoxesToText({x, SuperscriptBox(FractionBox("1", y))})

innerBoxesToText(SuperscriptBox(~x))::= "^" ++ parenthesize(x)
innerBoxesToText({~x, SubscriptBox(~y)})::= "Subscript(" ++ x ++ ", " ++ y ++ ")"
innerBoxesToText(SubscriptBox(~y))::= "Subscript(\"\", " ++ y ++ ")"
innerBoxesToText({~x, SubsuperscriptBox(~y,~z)})::= "Subscript(" ++ x ++ ", " ++ y ++ ")^" ++ parenthesize(z)
innerBoxesToText(SubsuperscriptBox(~y,~z))::= "Subscript(\"\", " ++ y ++ ")^" ++ parenthesize(z)

innerBoxesToText(FractionBox(~x,~y))::= parenthesize(x) ++ "/" ++ parenthesize(y)

innerBoxesToText(UnderscriptBox(~x,~y))::= "Underscript(" ++ innerBoxesToText(x) ++ ", " ++ innerBoxesToText(y) ++ ")"
innerBoxesToText(OverscriptBox(~x,~y))::= "Overscript(" ++ innerBoxesToText(x) ++ ", " ++ innerBoxesToText(y) ++ ")"
innerBoxesToText(UnderoverscriptBox(~x,~y,~z))::= innerBoxesToText(OverscriptBox(UnderscriptBox(x, y), z))

innerBoxesToText(FrameBox(~x,~~~))::= "Framed(" ++ innerBoxesToText(x) ++ ")"
innerBoxesToText(StyleBox(~x,~~~))::= innerBoxesToText(x)
innerBoxesToText(TagBox(~x,~~~))::= innerBoxesToText(x)
innerBoxesToText(InterpretationBox(~x,~~~))::= innerBoxesToText(x)

innerBoxesToText(GridBox(~m,~~~))::= "{" ++ m.Map(
	Function({row}, 
		"{" ++ row.Map(Function({x}, 
			innerBoxesToText(x))).Row(", ").ToString ++ "}")).Row(",\n").ToString ++ "}"

innerBoxesToText(Section(BoxData(~x),~~~))::= innerBoxesToText(x)
innerBoxesToText(Section(~x,~~~))::= innerBoxesToText(x)

innerBoxesToText(SectionGroup(~s:List,~~~))::= s.Map(innerBoxesToText).Row("\n\n").ToString

innerBoxesToText(~x)::= x.RawBoxes.ToString /*"[$Failed : " ++ x.ToString ++ "]"*/

End()
