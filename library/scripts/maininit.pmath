

Begin("System`")

If(HoldComplete($BaseDirectory) === HoldComplete @@ {$BaseDirectory},
	$BaseDirectory:= DirectoryName($Input)
	)

$Path:= {$BaseDirectory , ToFileName({DirectoryName($BaseDirectory, 3)}, "modules"), "."}

AutoLoad

Protect(Normalize, RotationMatrix, ScalingMatrix, ReflectionMatrix, ShearingMatrix, LinearFractionalTransform, ReflectionTransform, RotationTransform, AffineTransform, TranslationTransform, ScalingTransform, ShearingTransform, TransformationFunction, TransformationMatrix)


End()


System`Util`latexToBoxes;

Begin("System`Private`")


$NewMessage:= Function({symbol, tag},
	Local({namespace, name, pattern, matches},
		Try(
			namespace:= Namespace(symbol);
			name:= SymbolName(symbol);
			If(namespace =!= "System`", name:= namespace ++ name);
			If(name === "General", 
				pattern:= name ++ "::" ++ tag ++ WordBoundary
				,
				pattern:= {"General", name} ++ "::" ++ tag ++ WordBoundary
				);
			matches:= FindList(ToFileName($BaseDirectory, If(tag === "usage", "usage.pmath", "messages.pmath")), pattern);
			matches.Scan(ToExpression)
			)
		),
	HoldAll)
Protect($NewMessage)


AutoLoad(~heldall:Hold, ~file)::= 
	Local({p},
		(
			p:= Unprotect @@ heldall;
			AutoLoadContinue(heldall, ~heldsymbol)::= (AutoLoadContinue(heldall, ~hs):=.; AutoLoadFinish(heldall, heldsymbol, file));
			heldall.Scan(
				Function(s, s ::= AutoLoadStart(Hold(s), heldall); SetAttributes(s, ReadProtected), {HoldAll}));
		).Finally(Protect @@ p))
Attributes(Autoload):= {Protected, ReadProtected}

AutoLoadStart(~heldsymbol, heldall: Hold(~~~all))::= Synchronize({all}, AutoLoadContinue(heldall, heldsymbol))
AutoLoadContinue(~heldall, Hold(~sym))::= sym
AutoLoadFinish(heldall: Hold(~~~all), Hold(~sym), ~file)::= 
	Local({protections:= {}},
		(
			protections:= Unprotect(all);
			Clear(all);
			Get(file);
			sym
		).Finally(Protect @@ protections))


Unprotect(System`Utilities`GetSystemSyntaxInformation)
System`Utilities`GetSystemSyntaxInformation(~symbol:Symbol)::= 
	Local(
		{nsdata},
		Try(
			If(HoldComplete(SyntaxInformationData) === HoldComplete @@ {SyntaxInformationData}
			|| SyntaxInformationData === $Failed,
				SyntaxInformationData:= Get(ToFileName($BaseDirectory, "syntaxinformation.pmath"))
				);
			
			nsdata:= SyntaxInformationData.Cases({Namespace(symbol),~});
			If(nsdata.Length() > 0,
				nsdata:= nsdata[All,2].Flatten(1).Cases({SymbolName(symbol),~});
				If(nsdata.Length() > 0,
					Return(nsdata[All,2].Flatten(1))
					)
				)
			)
		)
Protect(System`Utilities`GetSystemSyntaxInformation)

Protect(System`Util`latexToBoxes)


Unprotect(Setting, System`Setting`$Rules);
System`Setting`$Rules:= {}
Setting(~expr, ~level)::= ReplaceRepeated(expr, System`Setting`$Rules, level);
Setting(~expr        )::= ReplaceRepeated(expr, System`Setting`$Rules);
SetAttributes(Setting, {Protected, ReadProtected});
Protect(System`Setting`$Rules);


Unprotect(ControlActive, $ControlActiveSetting);
$ControlActiveSetting:= False
ControlActive()::= $ControlActiveSetting
ControlActive(~t, ~f)::= If($ControlActiveSetting, t, f, f)
SetAttributes({ControlActive, $ControlActiveSetting}, {Protected, ReadProtected});


End()

AutoLoad(Hold(N), 
	ToFileName({$BaseDirectory, "auto", "core"}, "approx.pmath"))
AutoLoad(Hold(DynamicLocal, DynamicLocalBox), 
	ToFileName({$BaseDirectory, "auto", "core"}, "dynamiclocal.pmath"))
AutoLoad(Hold(ArcCos,ArcCot,ArcCsc,ArcSec,ArcSin,ArcTan, Cos,Cot,Csc,Sec,Sin,Tan, ArcCosh,ArcCoth,ArcCsch,ArcSech,ArcSinh,ArcTanh, Cosh,Coth,Csch,Sech,Sinh,Tanh), 
	ToFileName({$BaseDirectory, "auto", "core"}, "trig.pmath"))
AutoLoad(Hold(BernoulliB,Gamma,LogGamma), 
	ToFileName({$BaseDirectory, "auto", "core"}, "gammazeta.pmath"))
AutoLoad(Hold(Cross, Transpose, QRDecomposition), 
	ToFileName({$BaseDirectory, "auto", "core"}, "linalg.pmath"))
AutoLoad(Hold(Interval), 
	ToFileName({$BaseDirectory, "auto", "core"}, "interval.pmath"))
AutoLoad(Hold(Developer`InspectBoxes), 
	ToFileName({$BaseDirectory, "auto", "core"}, "Developer.pmath"))
AutoLoad(Hold(Normalize, RotationMatrix, ScalingMatrix, ReflectionMatrix, ShearingMatrix, LinearFractionalTransform, ReflectionTransform, RotationTransform, AffineTransform, TranslationTransform, ScalingTransform, ShearingTransform, TransformationFunction, TransformationMatrix),
	ToFileName({$BaseDirectory, "auto", "core"}, "geotrans.pmath"))

AutoLoad(Hold(System`Util`latexToBoxes), 
	ToFileName({$BaseDirectory, "auto", "core", "format"}, "latextoboxes.pmath"))

Get(ToFileName({$BaseDirectory, "auto", "core"}, "colors.pmath"));
Get(ToFileName({$BaseDirectory, "auto", "core", "format"}, "rawboxes.pmath"));
Get(ToFileName({$BaseDirectory, "auto", "core", "format"}, "accents.pmath"));
Get(ToFileName({$BaseDirectory, "auto", "core", "format"}, "brackets.pmath"));
Get(ToFileName({$BaseDirectory, "auto", "core", "format"}, "sumprod.pmath"));
Get(ToFileName({$BaseDirectory, "auto", "core", "graphics"}, "init.pmath"))

$NamespacePath:= {"System`"}
