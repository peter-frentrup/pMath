
BeginPackage("AutoCompletion`")

AutoCompletion`AutoCompleteName
AutoCompletion`AutoCompleteNameNoNamespace
AutoCompletion`AutoCompleteNamespaceGet
AutoCompletion`AutoCompleteOther
AutoCompletion`AutoCompleteFullFilename

Begin("AutoCompletion`Private`")

joinLists():= {}
joinLists(~~ls)::= Join(ls)

Function AutoCompleteName(~fullnames:List, ~part:String) {
	With(np:= namespacePattern(part))
	With(namespaces:= Union(fullnames.StringCases(~~~ns ++ "`" :> ns, 1).Apply(joinLists), allNamespacesFromPath()))
	With(byPrefix:= fullnames.selectByPrefix(np, part))
	With(byCamelCase:= fullnames.selectByCamelCase(np, part))
	With(nsByPrefix:= namespaces.selectByPrefix(StartOfString, part).Map(# ++ "`" &))
	
	Return(Join(
		byPrefix.makeShortNameList(part).Sort()[All,2],
		nsByPrefix, 
		byCamelCase.Complement(byPrefix).makeShortNameList(part).Sort()[All,2]))
}

Function AutoCompleteNameNoNamespace(~fullnames:List, ~part:String) {
	With(np:= namespacePattern(part))
	With(byPrefix:= fullnames.selectByPrefix(np, part))
	With(byCamelCase:= fullnames.selectByCamelCase(np, part))
	
	Return(Join(
		byPrefix.makeShortNameList(part).Sort()[All,2],
		byCamelCase.Complement(byPrefix).makeShortNameList(part).Sort()[All,2]))
}

Function AutoCompleteNamespaceGet(~part:String) {
	Return(allNamespacesFromPath().selectByCamelCase(StartOfString, part).Map(# ++ "`" &))
}

Function AutoCompleteOther(~list:List, ~part:String) {
	With(byPrefix:= list |> StringCases(StartOfString ++ part ++ ~~~, 1, IgnoreCase -> True) |> Apply(joinLists))
	With(byCamelCase:= list |> StringCases(StartOfString ++ makeCamelCaseOtherPattern(part) ++ ~~~, 1) |> Apply(joinLists))

	Return(Join(
		byPrefix |> Sort, 
		byCamelCase |> Complement(byPrefix) |> Sort))
}

Function AutoCompleteFullFilename(~part:String) {
	With(split:= part.StringCases(~~~dir ++ ("/"|$PathnameSeparator) ++ (file:Except("/"|$PathnameSeparator)***) :> {dir,file}))
	
	If(split.Length =!= 1) {
		Return({})
	}
	Return(FileNames(split[1,1], split[1,2] ++ "*"));
}
	
	
Function namespacePattern(~part:String) {
	If(part.containsSubstring("`")) {
		Return(StartOfString)
	} Else {
		Return(StartOfString ++ Prepend($NamespacePath, $Namespace))
	}
}

Function selectByPrefix(~list, ~np, ~part) {
	Return(list.StringCases(
		np ++ part ++ Except("`")*** ++ EndOfString, 
		1, 
		IgnoreCase -> True).Apply(joinLists))
}

Function selectByCamelCase(~list, ~np, ~part) {
	Return(list.StringCases(
		np ++ makeCamelCaseNamePattern(part) ++ Except("`")*** ++ EndOfString,
		1).Apply(joinLists))
}

Function allNamespacesFromPath() {
	Return(Union(
		$Path
			.FileNames(WordCharacter*** ++ ".pmath")
			.StringCases(
				$PathnameSeparator ++ (s: ("A".."Z" ++ Except($PathnameSeparator)**)) ++ ".pmath" :> s,
				1)
			.Apply(joinLists),
		$Path
			.FileNames(WordCharacter***)
			.FileNames("init.pmath")
			.StringCases(
				$PathnameSeparator ++ (s: ("A".."Z" ++ Except($PathnameSeparator)**)) ++ $PathnameSeparator ++ "init.pmath" :> s,
				1)
			.Apply(joinLists)
		))
}
	
% stringLength(~str:String)::= str.StringPosition(EndOfString)[1,-1]

Function makeCamelCaseNamePattern(~str:String) {
	With(interWordPattern:= Shortest(Except("`")***))
	Return(str.StringReplace({
		(StartOfString|RegularExpression("(?<=`)")) ++ c: RegularExpression("[a-zA-Z]") :> With(rc:= escapeRegex(c)) { RegularExpression("(?i:^" ++ rc ++ ")") | RegularExpression("(?i:[^a-z`]" ++ rc ++ ")") | upperCaseChar(c) },
		c: RegularExpression("[a-zA-Z]") :>  With(rc:= escapeRegex(c)) { c | (interWordPattern ++ (RegularExpression("(?i:[^a-z`]" ++ rc ++ ")") | upperCaseChar(c))) },
		c: RegularExpression("[^a-zA-Z]") :> interWordPattern ++ c}))
}

makeCamelCaseOtherPattern(~str:String)::= str |> StringReplace({
	RegularExpression("(?<=[a-zA-Z])(.)") :> "$1" | (Longest(Except(upperCaseChar("$1"))***) ++ upperCaseChar("$1")),
	~c :> charOrUpper(c)})

Function escapeRegex(~str:String) {
	Return(str.StringReplace(c: Characters("\[RawBackslash]^$.[|()?*+{") :> "\[RawBackslash]" ++ c))
}

charOrUpper(~c:String)::= With(u:=upperCaseChar(c)){ If(u===c, c, c|u) }

upperCaseChar("a"):= "A"
upperCaseChar("b"):= "B"
upperCaseChar("c"):= "C"
upperCaseChar("d"):= "D"
upperCaseChar("e"):= "E"
upperCaseChar("f"):= "F"
upperCaseChar("g"):= "G"
upperCaseChar("h"):= "H"
upperCaseChar("i"):= "I"
upperCaseChar("j"):= "J"
upperCaseChar("k"):= "K"
upperCaseChar("l"):= "L"
upperCaseChar("m"):= "M"
upperCaseChar("n"):= "N"
upperCaseChar("o"):= "O"
upperCaseChar("p"):= "P"
upperCaseChar("q"):= "Q"
upperCaseChar("r"):= "R"
upperCaseChar("s"):= "S"
upperCaseChar("t"):= "T"
upperCaseChar("u"):= "U"
upperCaseChar("v"):= "V"
upperCaseChar("w"):= "W"
upperCaseChar("x"):= "X"
upperCaseChar("y"):= "Y"
upperCaseChar("z"):= "Z"
upperCaseChar(~c)::= c

containsSubstring(~str:String, ~sub:String)::= str.StringPosition(sub, 1).Length > 0

Function makeShortNameList(~list:List, ~part:String) {
	If(StringPosition(part, "`", 1).Length > 0) {
		Return(list.Map({#,#}&))
	} Else {
		Return(list.Map(makeShortName))
	}
}
Function makeShortName(~fullname:String) {
	Return(fullname.StringReplace({
			long: (~~~ ++ "`" ++ (short: Except("`")**)) :> 
				{short, 
					If(Names(short).Length===1) { 
						short
					} Else {
						long
					}
				},
			~~long :> {long, long}
		},
		IgnoreCase -> True))
}


End()

EndPackage()
