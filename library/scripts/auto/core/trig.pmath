/* inverse trigonometric functions ArcCos, ArcCot, ArcCsc, ArcSec, ArsSin, ArcTan
 */

Begin("System`Private`")

(Pi*{1/3,1/4,1/5,2/5,1/6,1/10,3/10,1/12,5/12}).Map((ArcSin(Sin(#)):= #)&)
(Pi*{1/3,1/4,1/5,2/5,1/6,1/10,3/10,1/12,5/12}).Map((ArcTan(Tan(#)):= #)&)

ArcCsc(~x /? x=0):= ComplexInfinity
ArcCsc(~x)::= With({y:= ArcSin(1/x)}, y /? y.IsFreeOf(ArcSin))

ArcCos(~x)::= With({y:= ArcSin(x)}, Pi/2 - y /? y.IsFreeOf(ArcSin) && y.IsFreeOf(ArcSinh))
ArcSec(~x)::= With({y:= ArcCsc(x)}, Pi/2 - y /? y.IsFreeOf(ArcCsc) && y.IsFreeOf(ArcCsch))

/* Mathematica and MuPAD define ArcCot(x) as ArcTan(1/x), Maple and Derive as Pi/2 - ArcTan(x). These differ for non-positive x! */
ArcCot(~x)::= With({y:= ArcTan(x)}, Pi/2 - y /? y.IsFreeOf(ArcTan) && y.IsFreeOf(ArcTanh))
/* ArcCot(~x /? x=0):= Pi/2
ArcCsc(~x)::= With({y:= ArcTan(1/x)}, y /? y.IsFreeOf(ArcTan))*/

Cot(~x)::= With({y:= Tan(x)}, If(Sign(y)===0, ComplexInfinity, 1/y) /? y.IsFreeOf(Tan))
Csc(~x)::= With({y:= Sin(x)}, If(Sign(y)===0, ComplexInfinity, 1/y) /? y.IsFreeOf(Sin))
Sec(~x)::= With({y:= Cos(x)}, If(Sign(y)===0, ComplexInfinity, 1/y) /? y.IsFreeOf(Cos))

Coth(~x)::= With({y:= Tanh(x)}, If(Sign(y)===0, ComplexInfinity, 1/y) /? y.IsFreeOf(Tanh))
Csch(~x)::= With({y:= Sinh(x)}, If(Sign(y)===0, ComplexInfinity, 1/y) /? y.IsFreeOf(Sinh))
Sech(~x)::= With({y:= Cosh(x)}, If(Sign(y)===0, ComplexInfinity, 1/y) /? y.IsFreeOf(Cosh))

Cos /: Cos(~x)^(~n?IsInteger /? n < 0)::= Sec(x)^-n
Cot /: Cot(~x)^(~n?IsInteger /? n < 0)::= Tan(x)^-n
Csc /: Csc(~x)^(~n?IsInteger /? n < 0)::= Sin(x)^-n
Sec /: Sec(~x)^(~n?IsInteger /? n < 0)::= Cos(x)^-n
Sin /: Sin(~x)^(~n?IsInteger /? n < 0)::= Csc(x)^-n
Tan /: Tan(~x)^(~n?IsInteger /? n < 0)::= Cot(x)^-n

Cosh /: Cosh(~x)^(~n?IsInteger /? n < 0)::= Sech(x)^-n
Coth /: Coth(~x)^(~n?IsInteger /? n < 0)::= Tanh(x)^-n
Csch /: Csch(~x)^(~n?IsInteger /? n < 0)::= Sinh(x)^-n
Sech /: Sech(~x)^(~n?IsInteger /? n < 0)::= Cosh(x)^-n
Sinh /: Sinh(~x)^(~n?IsInteger /? n < 0)::= Csch(x)^-n
Tanh /: Tanh(~x)^(~n?IsInteger /? n < 0)::= Coth(x)^-n

End()
