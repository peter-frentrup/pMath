
BeginPackage("Developer`")

Developer`ConsoleMessageFormatter
Developer`DescribeLocation
Developer`PrintErrorDetails

Begin("Developer`Private`")

$errorID:= 0
Function nextErrorID() {
	Synchronize({$errorID}, $errorID+= 1; $errorID)
}

Internal`$MessageFormatter := ConsoleMessageFormatter

Function ConsoleMessageFormatter(~heldname, ~msg) {
 	% stack ends with {..., With} where With comes from here
	With(stack:= Stack())
	With(eid:= nextErrorID())
	PrintErrorDetails(eid)::=
		SectionPrint("Message", Row @ {errorButton(eid), "\n", ColonForm(heldname, msg), "\n at ", stack[..-2].Reverse.Map(stackEntryForm).Row("\n at ")})
	
	ColonForm(heldname, Row({msg, errorButton(eid)}, " "))
}

Function errorButton(~eid) {
	With(label:= Row @ {"[",eid,"]"})

	If($HyperlinksSupported) {
		Button(label, Developer`PrintErrorDetails(eid), BaseStyle -> "Hyperlink")
	} Else {
		label
	}
}

Options(stackEntryForm)::= {"Head" -> "?", "Location" -> None}
Function stackEntryForm(OptionsPattern()) {
	With(head:= OptionValue("Head"))
	With(location:= OptionValue("Location"))
	
	If(location === None) {
		Return( head )
	}
	Return( Row @ {head, " in ", location} )
}

Unprotect(Language`SourceLocation)
Format(~src:Language`SourceLocation)::= With({fmt:= DescribeLocation(src)}, fmt /? fmt =!= $Failed )
Protect(Language`SourceLocation)


DescribeLocation(~expr)::= $Failed
Function DescribeLocation(
	src: Language`SourceLocation(
		~filename:String, 
		{~fromLine:Integer, ~fromColumn: Integer} .. {~toLine:Integer, ~toColumn: Integer})
) {
	With(pathElements:= FileNameSplit(filename))
	With(shortname:= If(Length(pathElements) >= 2, pathElements[-1], filename))
	With(makeLongDesc:= If(Length(pathElements) >= 2, Function(StringForm("`1` (`2`)", #, ToFileName(pathElements[..-2]))), Identity))
	With(func:= If($HyperlinksSupported, Developer`SourceLocationOpenerFunction(src), None))
	
	With(makeButton:= If(func =!= None, Function(Button(#, func(), BaseStyle->"Hyperlink")).Replace(HoldPattern(Function(~body)()) :> body), Identity))

	If(fromLine === toLine) {
		If(fromColumn === toColumn) {
			Return( makeLongDesc @ makeButton @ StringForm("line `1`:`2` of `3`", fromLine, fromColumn, shortname) )
		}
		Return( makeLongDesc @ makeButton @ StringForm("line `1`: `2`..`3` of `4`", fromLine, fromColumn, toColumn, shortname) )
	}
	Return( makeLongDesc @ makeButton @ StringForm("lines `1`:`2` .. `3`:`4` of `5`", fromLine, fromColumn, toLine, toColumn, shortname) )
}


End()

EndPackage()
